
import (
	"th.um"
	"ent.um"
	"image.um"
	"signal.um"

	"global.um"
)

const (
	small* = 0
	medium*
	big*	

	baseOff* = th.Vf2{3, 7}
)

type Cactus* = struct {
	e: ^ent.Ent
	t: int
	// when water level reaches 1, cactus is updated
	waterLevel: th.fu
	held: bool
}

var (
	scene*: []^ent.Ent
	cacti*: []Cactus	

	images: []image.Image
)

fn spawn*(x: th.fu)

fn (c: ^Cactus) water(amount: th.fu) {
	c.waterLevel += amount
	if c.waterLevel >= 1 && c.t < big {
		c.waterLevel = 0
		c.t++
	}
}

fn init*() {
	images = make([]image.Image, 3)
	images[small] = image.load("gfx/cactus-small.png")
	images[medium] = image.load("gfx/cactus-medium.png")
	images[big] = image.load("gfx/cactus-big.png")
	global.bigCactusImage = images[big]

	scene = []^ent.Ent{}
	cacti = []Cactus{}

	signal.register("spawn_cactus", signal.Callback{
		spawn(^real(ctx)^)
	})

	signal.register("water_cactus", signal.Callback{
		args := ^[]th.fu(ctx)^
		cacti[round(args[0])].water(args[1])
	})
}

fn spawn*(x: th.fu) {
	y := global.cam.h/2 - global.cam.h*global.bottomPart - images[0].getDims().y
	c := Cactus{}
	c.e = new(ent.Ent)
	c.e^ = ent.mk(images[small], th.Transform{p: th.Vf2{x, y}, s: th.Vf2{1, 1}})
	c.t = small
	c.e.t.o = baseOff

	cacti = append(cacti, c)
	scene = append(scene, c.e)
}

fn handle*() { }

fn draw*() {
	for c in cacti {
		c.e.i = images[c.t]
		c.e.draw(global.cam)
	}	
}
